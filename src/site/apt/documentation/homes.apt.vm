 -----
 Uqbar Project
 -----
 ${project.name}
 -----

Homes

 Los objetos repositorio o <<Homes>> son los responsables de 

 * agregar
 
 * eliminar
 
 * actualizar
 
 * y consultar de diferentes maneras los objetos de dominio
 
 []
   

 Los homes de tu aplicación deben
 
 * ser serializables y 

 * extender de PersistentHome<T> donde T es el tipo de objeto que estás guardando en el home.

 []

 También tenés que definirle los métodos Class<T> getEntityType() y T createExample() en las subclases concretas 
 (ojo que si vas a definir una clase abstracta AbstractMiAplicacionHome que unifique comportamiento 
 igualmente debés dejar que las subclases de ese Home redefinan ambos métodos ya que no conocés la
 clase concreta de T).
 
 Los homes de tu aplicación te van a dar la forma de acceder a los datos. Al 
 extender de PersistentHome tus Homes van a entender los siguientes mensajes:

 * <<List<T> allInstances()>>: Devuelve todas las instancias persistidas.

 * <<void create(T object)>>: Persiste un nuevo objeto en la base de datos. 

 * <<void update(T object)>>: Actualiza la información de la base de datos 
 de un objeto ya persistido previamente.
 
 * <<void delete(T object)>>: Elimina de la base de datos el objeto pasado.
 
 * <<T searchById(int id)>>: Busca y devuelve un solo objeto con el id pasado por parámetro.

 * <<List<T> searchByExample(T example)>>: Devuelve una lista con los objetos del mismo tipo del ejemplo, en la lista van a estar los objetos que tengan los mismos atributos no nulos que el ejemplo tenga. 
   Ej: Si paso un example de Persona con solamente el nombre distinto de null, por ejemplo "Pablo", me va a traer todas las personas con ese nombre.
   <<Los queries by example sobre relaciones no está soportada por el momento.>> 

 []

 Atención con el uso de estos mensajes, una buena distribución de responsabilidades estaría pensada para que los homes de tu aplicación le den servicios de mayor nivel y la aplicación use algo más
 que un searchByExample.


* Restricciones de la búsqueda by example

 Si una película tiene un objeto género, no se puede hacer la búsqueda by example pasando como prototipo una película con un objeto género 
 
+--
new Pelicula => [
    genero = new Genero("terror")
]
+--

 para buscar las películas de terror. Esto mismo vale para colecciones de objetos.
 
    
Definición

 Para definir un home de celulares, la definición del repositorio en Java sería

+--
@Observable
public class RepositorioCelulares extends PersistentHome<Celular> implements Serializable {
+--

 El mismo ejemplo en Xtend es:
  
+--
@Observable
class RepositorioCelulares extends PersistentHome<Celular> implements Serializable {
+--

Ejemplo

* Xtend

 A continuación mostramos cómo sería un objeto home o repositorio de clientes de una compañía de celulares:
 
+--
class RepoCelulares extends PersistentHome<Celular> {

	override def getEntityType() {
		typeof(Celular)
	}

	override def createExample() {
		new Celular
	}

	// ***********************************************************
	// ** Inicialización y armado de un fixture o juego de datos
	// ***********************************************************
	new() {
		this.init
	}

	def void init() {
		this.createIfNotExists("Laura Iturbe", 88022202, getModelo("NOKIA LUMIA 625"), false)
		...
	}

	// ********************************************************
	// ** Altas y bajas
	// ********************************************************
	override create(Celular celular) {
		celular.validar
		validarClientesDuplicados(celular)
		super.create(celular)
	}

	def void validarClientesDuplicados(Celular celular) {
		val numero = celular.numero
		if (!this.search(numero).isEmpty) {
			throw new UserException("Ya existe un celular con el número: " + numero)
		}
	}

	def createIfNotExists(Celular celular) {
		var celularDB = this.get(celular.numero)
		if (celularDB == null) {
			this.create(celular)
			celularDB = celular
		}
		celularDB
	}

	def void createIfNotExists(String pNombre, Integer pNumero, ModeloCelular pModeloCelular,
		Boolean pRecibeResumenCuenta) {
		var celular = new Celular => [
			nombre = pNombre
			numero = pNumero
			modeloCelular = pModeloCelular
			recibeResumenCuenta = pRecibeResumenCuenta
		]
		this.createIfNotExists(celular)
	}

	// ********************************************************
	// ** Búsquedas
	// ********************************************************
	/**
	 * Devuelve un celular en base al número (que no puede repetirse)
	 */
	def Celular get(Integer unNumero) {
		val celulares = this.search(unNumero)
		if (celulares.isEmpty) {
			null
		} else {
			celulares.get(0)
		}
	}

	/**
	 * Hace la búsqueda de un celular únicamente por número
	 */
	def search(Integer numero) {
		this.search(numero, null)
	}

	/**
	 * Busca los celulares que coincidan con los datos recibidos. Tanto número como nombre pueden ser nulos,
	 * en ese caso no se filtra por ese atributo.
	 *
	 * Actualmente la búsqueda se hace by example pero requiere que sea exactamente ese número o ese nombre
	 */
	def search(Integer unNumero, String unNombre) {
		searchByExample(
			new Celular => [
				numero = unNumero
				nombre = unNombre
			])
	}

}
+--

  Otra posibilidad cuando no hay un gran volumen de datos es traer el grafo de objetos a memoria y hacer el filtro
  en forma manual:
  
+--
	/**
	 * Para soportar búsquedas por substring hay que descomentar todo el código de abajo, el problema es que trae 
	 * a memoria todo el grafo de celulares (con una cantidad enorme de celulares puede traer problemas de performance)
	 * En ese caso el celular (12345, "Juan Gonzalez") será contemplado por la búsqueda (23, "Gonza")
	 * 
	 */
	def search(Integer unNumero, String unNombre) {
		allInstances.filter[celular|this.match(numero, celular.numero) && this.match(nombre, celular.nombre)].toList
	}
	
	def match(Object expectedValue, Object realValue) {
		if (expectedValue == null) {
			return true
		}
		if (realValue == null) {
			return false
		}
		realValue.toString().toLowerCase().contains(expectedValue.toString().toLowerCase())
	}
+--
    
* Java

 Veamos un ejemplo similar utilizando Java como lenguaje de programación:
 
+--
// TODO
+--     